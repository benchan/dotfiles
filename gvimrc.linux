" GUI オンリーなプラグインのロード
NeoBundleSource open-browser.vim
NeoBundleSource twibill.vim
NeoBundleSource webapi-vim
NeoBundleSource TweetVim
NeoBundleSource neco-tweetvim
NeoBundleSource restart.vim

colorscheme molokai
" IM の起動キーを gVim に教える
" set imactivatekey=S-space
set iminsert=2
set vb t_vb=
" フォント設定
set guifont=Migu\ 2M\ 14
set guioptions-=e

let g:openbrowser_open_commands = ['google-chrome', 'xdg-open', 'w3m']
if !exists('g:openbrowser_open_rules')
    let g:openbrowser_open_rules = {}
endif
let g:openbrowser_open_rules['google-chrome'] = "{browser} {shellescape(uri)}"

let g:tweetvim_display_icon = 1
let g:tweetvim_tweet_per_page = 40

nnoremap <silent><Leader>tw :<C-u>tabnew <Bar> TweetVimHomeTimeline<CR>
nnoremap <silent><Leader>tl :<C-u>TweetVimHomeTimeline<CR>
nnoremap <silent><Leader>tm :<C-u>TweetVimMentions<CR>
nnoremap <Leader>say :<C-u>TweetVimSay<CR>

augroup TweetVimMapping
    autocmd!
    autocmd FileType tweetvim_say nnoremap <buffer><silent><C-g> :<C-u>q!<CR>
    autocmd FileType tweetvim_say inoremap <buffer><silent><C-g> <C-o>:<C-u>q!<CR><Esc>
    autocmd FileType tweetvim_say imap <buffer><C-CR> <Esc><Plug>(tweetvim_action_enter)
    autocmd FileType tweetvim nnoremap <buffer>s :<C-u>TweetVimSay<CR>
    autocmd FileType tweetvim nnoremap <buffer>m :<C-u>TweetVimMentions<CR>
    autocmd FileType tweetvim nmap <buffer>c <Plug>(tweetvim_action_in_reply_to)
    autocmd FileType tweetvim nnoremap <buffer>t :<C-u>Unite tweetvim -no-start-insert -quick-match<CR>
    autocmd FileType tweetvim nmap <buffer><Tab> <Plug>(tweetvim_action_reload)
    autocmd FileType tweetvim nmap <buffer><silent>gg gg<Plug>(tweetvim_action_reload)
    autocmd FileType tweetvim nmap <buffer>f <Plug>(tweetvim_action_page_next)
    autocmd FileType tweetvim nmap <buffer>b <Plug>(tweetvim_action_page_previous)
    autocmd FileType tweetvim nmap <buffer><Leader>F <Plug>(tweetvim_action_remove_favorite)
    autocmd FileType tweetvim nmap <buffer><Leader>d <Plug>(tweetvim_action_remove_status)
    autocmd FileType tweetvim nnoremap <buffer><Leader><Leader> :<C-u>call <SID>tweetvim_favstar()<CR>
    autocmd FileType tweetvim nnoremap <buffer><Leader>u :<C-u>call <SID>tweetvim_open_home()<CR>
    autocmd FileType tweetvim nunmap <buffer>ff
    autocmd FileType tweetvim nunmap <buffer>bb
    autocmd TabEnter * call <SID>tweetvim_reload()
    autocmd BufLeave * call <SID>tweetvim_say_leave()
augroup END

function! s:tweetvim_reload()
    if &filetype ==# "tweetvim"
        call feedkeys("\<Plug>(tweetvim_action_reload)")
    endif
endfunction

function! s:tweetvim_say_leave()
    if &filetype ==# "tweetvim_say"
        call feedkeys("\<Plug>(tweetvim_action_reload)")
    endif
endfunction

function! s:tweetvim_favstar()
    let screen_name = matchstr(getline('.'),'^\s\zs\w\+')
    let path = empty(screen_name) ? "/me" : "/users/" . screen_name

    execute "OpenBrowser http://ja.favstar.fm" . path
endfunction

function! s:open_favstar()
    let username = expand('<cword>')
    if empty(username)
        OpenBrowser http://ja.favstar.fm/me
    else
        execute "OpenBrowser http://ja.favstar.fm/users/" . username
    endif
endfunction
command! Favstar call <SID>open_favstar()

function! s:tweetvim_open_home()
    let username = expand('<cword>')
    if username =~# '^[a-zA-Z0-9_]\+$'
        execute "OpenBrowser https://twitter.com/" . username
    endif
endfunction

nmap <Leader>u <Plug>(openbrowser-open)
vmap <Leader>u <Plug>(openbrowser-open)

" vim: set ft=vim fdm=marker ff=unix fileencoding=utf-8
